###
	Copyright 2013 Robert Kunze

	Licensed under the Apache License, Version 2.0 (the "License");
	you may not use this file except in compliance with the License.
	You may obtain a copy of the License at

	    http://www.apache.org/licenses/LICENSE-2.0

	Unless required by applicable law or agreed to in writing, software
	distributed under the License is distributed on an "AS IS" BASIS,
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
	See the License for the specific language governing permissions and
	limitations under the License.
###
include_once dirname(!__FILE__) + "/snowcompiler.php"
outputFile = null
interactive = true
compileOnly = false
watch = false
LASTFAIL = 999999999999999

fn watchChanges(dir, callback)
	dp = opendir dir
	if not dp
		throw new Exception("Unable to open directory {dir}.")
	while (file = readdir(dp)) isnt false
		path = "{dir}/{file}"
		oldtime = filemtime path
		newfile = path->replace(/\.snow$/, '.php')
		if /\.snow$/->preg_match(file) isnt false and (not file_exists(newfile) or filemtime(path) > filemtime(newfile) or oldtime > LASTFAIL)
			echo "\nCompiling {path}..."
			try
				callback->call_user_func(dir + '/' + file)
				echo " OK"
				LASTFAIL = 999999999999999
			catch e
				echo "\n" + e.getMessage()
				echo " FAILED"
				file_put_contents newfile, time()
				LASTFAIL = time()
		else if is_dir(path) is true and file[0] isnt '.'
			watchChanges(dir + '/' + file, callback)
	closedir dp

fn compile(file)
	snow = new SnowCompiler(file->file_get_contents())
	result = do snow.compile
	file->replace(/\.snow$/, '.php')->file_put_contents "<?php\n{result};\n?>"

if count(argv) > 1
	result = getopt "o:c:h"
	
	if count(result) <= 0
		for val, i in argv
			if i > 0
				if val[0] is '-'
					result[val[1]] = ""
				else
					code = file_get_contents val
					interactive = false

	if result['o']??
		outputFile = result['o']
	if result['c']??
		code = file_get_contents result['c']
		interactive = false
		compileOnly = true
	else if result['c']?
		compileOnly = true
	else if result['w']?
		watch = true

	if result['h']?
		ver = SnowCompiler..version
		help = """
Snow Script Compiler Version {ver}

Syntax:
	{argv[0]} [-o <output file>] [-chw] [<input file>]

	-o <output file> - write the compiled result into the given file. If this parameter is missing, the compiled result will be written to stdout.
	-c               - the input should only be compiled - not executed
	-h               - shows this help
	-w		 - watch mode: compile any changed snow file in given directory tree
	<input file>     - the file that should be used as input. if this parameter is omitted, an interactive console will let you enter commands.
"""
		die help

if watch
	echo "Waiting for file changes..."
	while true is true
		watchChanges ".", 'compile'
		sleep 1
	do die

if interactive
	fp = fopen "php://stdin", 'r'
	input = ""
	while not feof fp
		input = input % fread(fp, 4096)
	code = input

snow = new SnowCompiler(code)
result = do snow.compile

if compileOnly is true
	result = "<?php\n{result};\n?>"
	if outputFile??
		file_put_contents outputFile, result
	else
		echo result
else if compileOnly is false
	echo "\n==> Result: \n"
	lines = ";"->explode(result)
	for i in count(lines) - 1 downto 0
		trimmed = trim lines[i]
		if not empty trimmed
			lines[i] = "return " + lines[i]
			break
	result = eval ";"->implode lines
	echo result
