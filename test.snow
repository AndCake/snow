# general example
fn out
	args = func_get_args()
	echo " "->implode(args)
	echo "\n"
	<- count args

fn how_big_is_it(number)
	if number < 100
		<- 'small'
	else
		<- 'big'

randomNumber = rand(0, 10)
randomNumber->how_big_is_it()->out(" is the number {randomNumber}.")

for i in 1 to randomNumber
	out "{i}: hallo"

# variables
fungus = "Sarcoscypha coccinea"

# constants
!DB_ENGINE = 'mysql'

# comparison
a is b and c != d
averageFeet = 1
fn myFeet <- 1.1
if myFeet() > averageFeet
	echo("BIGFOOT\n")

# comments
# single line comment
###
	multi line 
	comment
###

# quoted strings
fn travel
	echo "
		The {animal} went to {world.place()}
		with his {!NUM} friends.
	"

"""<a href="https://snowscript.{animal}">Snowscript</a>\n"""

'no {magic} here'

# string concatenation
myString = "test"
value = _SERVER['REQUEST_STRING'] + "\n"
echo "I am " + "legend!\n"
echo myString % value

# regular expressions
/^[a-z']+$/i->preg_match myvar, match
if match[0]??
	echo "It has only letters\n"

# lists
pianists = ["McCoy Tyner", "Fred Hersch", "Bill Evans"]
# => Fred Hersch
echo pianists[1] + "\n"
# => Fred Hersch, Bill Evans
echo pianists[1...2]->join(", ") + "\n"

# dictionaries
series = [
	'Heroes': [
		'genre': 'Science Fiction',
		'creator': 'Tim Kring',
		'seasons': 4,
	],
	'Game Of Thrones': [
		'genre': 'Medieval fantasy',
		'creator': 'David Benioff',
		'seasons': 2,
	]
]
echo series['Heroes']['genre'] + "\n"

# functions
fn findMessage(fancystring = "sample message")
	<- fancystring->ucfirst()

someResult = 2
print("Test\n")
var_dump fancyObject, someResult
message = do findMessage

# chaining
echo "peter"->ucfirst()->str_rot13() + "\n"

fn fight_valiantly <- out "fighting...\n"
fn improvise <- out "improvising...\n"
fn run <- out "running...\n"
fn try_this <- null

white_walkers = []
feeling_lucky = rand(0, 100) > 50
height = 175
veryHigh = 190
field = []

# control structures
if white_walkers->count() < 500
	do fight_valiantly
elif feeling_lucky
	do improvise
else
	do run
	if foundHideout()
		do hide
	else
		do run

# ternary operators
a = if height is veryHigh then "tall" else "small"

# existence 
if field['title']? or !RANDOM_STUFF?
	improvise()

stuff = if try_this()?? then "Something" else "Default"

# loops
flowers = ["Violet": ["id": 1], "Rose": ["id": 2]]
for data, title in flowers
	echo "{data['id']}: {title}\n"

for i in 1 to 10 step 2
	echo i + ", "
for i in 10 downto 1
	echo i + ", "

# while
i = 0
while i < 10
	i++
	echo "Rinse and repeat.\n"

# classes
class Air
	static a = 0
	static fn run
		<- "Air is running.\n"

class Wind extends Air

	fn Wind(speed, angle)
		Air..a = speed
		value = do Air..run
		@test = angle
		echo value

	static fn create(speed = 0, angle = 0)
		<- new Wind speed, angle

	fn blow()
		if @test * Air..a isnt 0
			<- @test * Air..a

wind = new Wind(52, 12)
do wind.blow

Wind..create(23, 1)
Wind..run()

# de-structuring assignments
a = 1
b = 5
c = 2
fn getLetters
	<- [1, 2, 3, 4]

[a,, c] = [b, c, a]
[a, b, [c, d]] = do getLetters
